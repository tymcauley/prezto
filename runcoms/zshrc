#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

###
# Modifications by Tynan
###

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

#
# Shell Behavior
#

# Turns out I like using '^' and '#' in normal shell use.
unsetopt EXTENDED_GLOB

# Send the HUP signal to jobs when the shell exits.
setopt HUP

# Report the status of background and suspended jobs before exiting.
setopt CHECK_JOBS

# Don't share history between all sessions.
unsetopt SHARE_HISTORY

# Short normal/insert mode switching
export KEYTIMEOUT=1

# Specify the ZSH command history file and size.
export HISTFILE="$HOME/.zhistory"
export HISTSIZE=20000
export SAVEHIST=20000

#
# Colors
#

# Enable colors in the shell
autoload -U colors
colors

#
# Custom Aliases
#

if command -v exa > /dev/null 2>&1 ; then
  # Replace ls with exa: https://the.exa.website/
  alias exa='exa --classify'
  alias ls='exa'
  alias ll='ls --long --binary --group'
  alias llg='ll --git'
  alias la='ll --all'
  alias l='ls --oneline --all'
  alias lr='ls --recurse'
  alias llr='ll --recurse'
  alias lt='ls --tree'
  alias llt='ll --tree'
  alias llst='ll --sort=newest' # Sort by modified time, most recent at the bottom
  alias llss='ll --sort=size'   # Sort by size, largest at the bottom
fi

if command -v nvim > /dev/null 2>&1 ; then
  alias vim='nvim'
  alias vimdiff='nvim -d'
fi

# Suggested edits from homebrew since ZSH is installed in a non-standard location.
if [[ "$OSTYPE" == darwin* ]]; then
  unalias run-help
  autoload run-help
  HELPDIR=/usr/local/share/zsh/help
fi

#
# fzf setup.
#

# If the fzf setup file exists, load it.
[[ -f ~/.fzf.zsh ]] && source ~/.fzf.zsh

if command -v fzf > /dev/null 2>&1 ; then
  # If the fzf setup file doesn't exist, then prompt the user to create it.
  [[ ! -f ~/.fzf.zsh ]] && echo "WARNING: Please generate the FZF setup file (use 'brew info fzf' for more information)"

  # Set default fzf search command.
  if command -v fd > /dev/null 2>&1 ; then
    export FZF_DEFAULT_COMMAND="fd --type f --hidden --follow"
    export FZF_CTRL_T_COMMAND="fd --type f --follow"
  fi
fi

#
# Key Bindings
#

if command -v fzf > /dev/null 2>&1 ; then
  # Search history with fzf if it is present.
  bindkey -M vicmd "/" fzf-history-widget

  # Use fzf to find files/directories.
  bindkey -M vicmd "gf" fzf-file-widget
  bindkey -M vicmd "gd" fzf-cd-widget
else
  # If fzf isn't installed, then just reverse the default search direction (use '/' to search backward in command
  # history, use '?' to search forward).
  bindkey -M vicmd "/" history-incremental-pattern-search-backward
fi

bindkey -M vicmd "?" history-incremental-pattern-search-forward

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# Load any other potential modifications to the shell.
[[ -f "$HOME/.zshrc.local" ]] && source "$HOME/.zshrc.local"
