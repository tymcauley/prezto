#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

###
# Modifications by Tynan
###

#
# Shell Behavior
#

# Turns out I like using '^' and '#' in normal shell use.
unsetopt EXTENDED_GLOB

# Send the HUP signal to jobs when the shell exits.
setopt HUP

# Report the status of background and suspended jobs before exiting.
setopt CHECK_JOBS

# Don't share history between all sessions.
unsetopt SHARE_HISTORY

# Short normal/insert mode switching
export KEYTIMEOUT=1

#
# Key Bindings
#

if command -v fzf > /dev/null 2>&1 ; then
  # Search history with fzf if it is present.
  bindkey -M vicmd "/" fzf-history-widget

  # Use fzf to find files/directories.
  bindkey -M vicmd "gf" fzf-file-widget
  bindkey -M vicmd "gd" fzf-cd-widget
else
  # If fzf isn't installed, then just reverse the default search direction (use '/' to search backward in command
  # history, use '?' to search forward).
  bindkey -M vicmd "/" history-incremental-pattern-search-backward
fi

bindkey -M vicmd "?" history-incremental-pattern-search-forward

#
# Colors
#

# Enable colors in the shell
autoload -U colors
colors

# Base16 Shell
BASE16_SHELL=$HOME/.config/base16-shell/
[ -n "$PS1" ] && [ -s $BASE16_SHELL/profile_helper.sh ] && eval "$($BASE16_SHELL/profile_helper.sh)"

#
# Custom Aliases
#

# Add symbols after pathnames that are directories, executables, links, etc.
alias ls="${aliases[ls]:-ls} -F"

# Suggested edits from homebrew since ZSH is installed in a non-standard location.
if [[ "$OSTYPE" == darwin* ]]; then
  unalias run-help
  autoload run-help
  HELPDIR=/usr/local/share/zsh/help
fi

#
# fzf setup.
#

if command -v fzf > /dev/null 2>&1 ; then
  # If fzf is installed by git, then auto-completion and key bindings have already been loaded.
  if [[ ! -f ~/.fzf.zsh ]] ; then
    # Load fzf features for ZSH. This should be a more cross-platform-friendly version of the ~/.fzf.zsh file.
    if [[ "$OSTYPE" == darwin* ]] ; then
      FZF_ZSH_COMPLETIONS="/usr/local/opt/fzf/shell/completion.zsh"
      FZF_ZSH_BINDINGS="/usr/local/opt/fzf/shell/key-bindings.zsh"
    else
      # These are the file locations in Debian. Haven't tested any other distros.
      FZF_ZSH_COMPLETIONS="/usr/share/zsh/vendor-completions/_fzf"
      FZF_ZSH_BINDINGS="/usr/share/doc/fzf/examples/key-bindings.zsh"
    fi

    if [[ -f "$FZF_ZSH_COMPLETIONS" ]] ; then
      source "$FZF_ZSH_COMPLETIONS"
    else
      echo "ERROR: Couldn't find fzf completions for zsh. Expected them at '$FZF_ZSH_COMPLETIONS'"
    fi

    if [[ -f "$FZF_ZSH_BINDINGS" ]] ; then
      source "$FZF_ZSH_BINDINGS"
    else
      echo "ERROR: Couldn't find fzf bindings for zsh. Expected them at '$FZF_ZSH_BINDINGS'"
    fi

    unset FZF_ZSH_COMPLETIONS
    unset FZF_ZSH_BINDINGS
  fi

  # Set default fzf search command.
  if command -v fd > /dev/null 2>&1 ; then
    export FZF_DEFAULT_COMMAND="fd --type f --hidden --follow"
    export FZF_CTRL_T_COMMAND="fd --type f --follow"
  fi
fi

#
# ripgrep setup.
#

if command -v rg > /dev/null 2>&1 ; then
  # Search for a pattern in a file, and open your editor to that file's location.
  # For vim, requires the vim-fetch plugin.
  sf () {
    printf -v search "%q" "$*"
    include="yml,js,json,php,md,styl,pug,jade,html,config,py,cpp,c,go,hs,rb,conf,fa,sv,svh,v,h"
    exclude=".config,.git,node_modules,vendor,build,yarn.lock,*.sty,*.bst,*.coffee,dist"
    rg_command='rg --column --line-number --no-heading --fixed-strings --ignore-case --no-ignore --hidden --follow --color "always" -g "*.{'$include'}" -g "!{'$exclude'}/*"'
    files=`eval $rg_command $search | fzf --ansi --multi --reverse | awk -F ':' '{print $1":"$2":"$3}'`
    [[ -n "$files" ]] && ${EDITOR:-vim} $files
  }
fi

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ -f ~/.p10k.zsh ]] && source ~/.p10k.zsh
