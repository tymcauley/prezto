#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

if command -v nvim > /dev/null 2>&1 ; then
    export EDITOR='nvim'
    export VISUAL='nvim'
else
    export EDITOR='vim'
    export VISUAL='vim'
fi
export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  $HOME/.local/bin
  $HOME/.local/share/coursier/bin
  $HOME/.cargo/bin
  /usr/local/{bin,sbin}
  $path
)

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

###
# Modifications by Tynan
###

# Set up modifications for Python interactive shell.
[[ -f "$HOME/.pystartup" ]] && export PYTHONSTARTUP="$HOME/.pystartup"

# Load any other potential modifications to the shell.
[[ -f "$HOME/.zprofile.local" ]] && source "$HOME/.zprofile.local"

# Load Homebrew path/environment modifications. Use the `BREW_BIN_PREFIX` prefix to the `brew`
# command if Homebrew is installed in a non-default location (you should set that inside of
# `$HOME/.zprofile.local`, and ensure it has a trailing slash).
command -v ${BREW_BIN_PREFIX:-}brew > /dev/null 2>&1 && eval $(${BREW_BIN_PREFIX:-}brew shellenv)

if [[ -n "$HOMEBREW_PREFIX" ]]; then
    # If LLVM is installed, add all of the LLVM utilities to the end of the path.
    [[ -d "$HOMEBREW_PREFIX/opt/llvm/bin" ]] && path=($path $HOMEBREW_PREFIX/opt/llvm/bin)

    # Add homebrew zsh completion files to fpath.
    fpath=($HOMEBREW_PREFIX/share/zsh/site-functions $fpath)
fi

# Add local directory for zsh completion files.
fpath=(
  $fpath
  $HOME/.zfunc
)

# On some systems (mostly Linux, where fd already exists), fd is installed as fdfind. Create an alias for that.
command -v fdfind > /dev/null 2>&1 && alias fd=fdfind
